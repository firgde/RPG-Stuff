# slot番目のスロットにストレージのindex番目のNPCを入れる
    $execute if predicate {"condition":"value_check","value":{"type":"storage","storage":"book:","path":"data.social.npc[$(index)].discovered"},"range":1} if data storage book: data.social.npc[$(index)] run loot replace entity @s container.$(slot) loot {"type":"command","pools":[{"rolls":1,"entries":[{"type":"item","name":"stick","functions":[{"function":"set_name","entity":"this","name":{"storage":"book:","nbt":"data.social.npc[$(index)].name","interpret":true,"italic":false},"target":"item_name"},{"function":"set_lore","entity":"this","lore":[{"storage":"book:","nbt":"data.social.npc[$(index)].description","interpret":true,"color":"gray","italic":false},{"translate":"social.ui.knowledge_book.status.social.npc.location","with":[{"nbt":"data.social.npc[$(index)].location","storage":"book:","interpret":true,"color":"gray"}],"color":"white","italic":false},{"translate":"social.ui.knowledge_book.status.social.npc.type","with":[{"nbt":"data.social.npc[$(index)].type","storage":"book:","interpret":true,"color":"gray"}],"color":"white","italic":false}],"mode":"replace_all"},{"function":"set_custom_data","tag":"{ui:1b,menu:\"knowledge_book\"}"}]}]}]}
# 発見済みならアイコン、そうでないなら空きスロット配置
    $execute if predicate {"condition":"value_check","value":{"type":"storage","storage":"book:","path":"data.social.npc[$(index)].discovered"},"range":1} run item modify entity @s container.$(slot) [{"function":"set_custom_model_data","floats":{"values":[{"type":"storage","storage":"book:","path":"data.social.npc[$(index)].custom_model_data"}],"mode":"replace_all"}}]
    $execute if predicate {"condition":"value_check","value":{"type":"storage","storage":"book:","path":"data.social.npc[$(index)].discovered"},"range":0} run loot replace entity @s container.$(slot) loot {"pools":[{"rolls":1,"entries":[{"type":"item","name":"stick","functions":[{"function":"set_name","entity":"this","target":"item_name","name":{"translate":"social.ui.knowledge_book.status.social.npc.undiscovered","color":"gray","italic":true}},{"function":"set_custom_data","tag":"{ui:1b,menu:\"knowledge_book\"}"},{"function":"set_components","components":{"item_model":"asset:npc/undiscovered"}}]}]}]}
# そもそもNPC存在しないなら空
    $execute unless data storage book: data.social.npc[$(index)] run item replace entity @s container.$(slot) with stick[item_model="air",custom_data={ui:1b,menu:"knowledge_book"},tooltip_display={hide_tooltip:true}]
